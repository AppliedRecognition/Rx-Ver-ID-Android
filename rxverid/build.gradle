apply plugin: 'com.android.library'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode rootProject.generateVersionCode()
        versionName rootProject.generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

task javadoc(type: Javadoc, dependsOn: ':doclet:compileJava') {
    source = android.sourceSets.main.java.srcDirs

    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"

    configurations.implementation.setCanBeResolved(true)

    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += files(ext.androidJar)

    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PUBLIC
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/Jama'
    options.doclet("AppliedRecDoclet")
    options.docletpath(new File("${project.getRootDir()}/doclet/build/classes/java/main"))
    options.addStringOption("htmlsrc", "${project.getRootDir()}/doclet/src/docs")
    options.addStringOption("destpath", "${project.getRootDir()}/docs")
    print("Generate documentation in ${project.getRootDir()}/docs")
    failOnError = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.appliedrec.verid:core:$rootProject.veridVersion"
    implementation "androidx.appcompat:appcompat:$rootProject.appcompatVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroidVersion"
    implementation "org.javatuples:javatuples:$rootProject.javatuplesVersion"
    implementation "androidx.annotation:annotation:$rootProject.annotationVersion"
    implementation "androidx.exifinterface:exifinterface:$rootProject.exifinterfaceVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.28.2"

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'org.mockito:mockito-android:2.28.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
